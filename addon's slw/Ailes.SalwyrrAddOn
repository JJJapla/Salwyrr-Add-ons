@Event: onKeyPressed:%pressedKey%
if:'%pressedKey%'='Lshift'
var:shift;1
var:div;4
endStructure

@Event: onKeyUnpressed:%unpressedKey%
if:'%unpressedKey%'='Lshift'
var:shift;0
var:div;1
var:-1;1
var:tick;0
endStructure

@Event: onAddonStart
var:X;0
var:Y;0
var:Z;0
var:div;1
var:shift;0
var:tick;0
var:-1;1
var:F5;0
var:key;F5
var:dejavu;0
var:on;1
getSave:key;key
getSave:dejavu;dejavu
getSave:on;on
if:'%dejavu%'='0'
save:1;dejavu
callMethod:help
endStructure

@Event: onTick
var:X;Math.cos((%playerHeadYaw%+270)/57.2958)/5*%div%
var:Z;Math.sin((%playerHeadYaw%+270)/57.2958)/5*%div%
increment:tick;%-1%
if:'%tick%'='0'
var:-1;1
endStructure
if:'%tick%'='20'
var:-1;-1
endStructure

@Event: onKeyPressed:%pressedKey%
if:'%pressedKey%'='%key%'
increment:F5;1
if:'%F5%'='3'
var:F5;0
endStructure
endStructure

#Method:help
addMessageIn:0.1;§6Criado por NexFord e traduzido por JJJapla !
addMessageIn:0.2;§a/asas on §7Activa as asas !
addMessageIn:0.3;§c/asas off §7Desactiva as asas !
addMessageIn:0.4;§9§B/asas key §7Modifique seu toque para mudar as visualizações !
addMessageIn:0.5;§7Para não ver as asas em primeira pessoa !
addMessageIn:0.6;§2/asas help §7Afixa estas mensagens !
@MethodEnd

@Event: onRender
if:'%on%'='1'
if:%F5%>0
if:'%shift%'='0'
draw3dItem:417;%playerPositionX%+(%X%);%playerPositionY%+0.4;%playerPositionZ%+(%Z%);%playerHeadYaw%+180-(%tick%);0;0;150
draw3dItem:417;%playerPositionX%+(%X%);%playerPositionY%+0.4;%playerPositionZ%+(%Z%);%playerHeadYaw%+%tick%;0;0;150
endStructure
if:'%shift%'='1'
draw3dItem:417;%playerPositionX%+(%X%);%playerPositionY%+0.4;%playerPositionZ%+(%Z%);%playerHeadYaw%+180;-30;0;150
draw3dItem:417;%playerPositionX%+(%X%);%playerPositionY%+0.4;%playerPositionZ%+(%Z%);%playerHeadYaw%;30;0;150
endStructure
endStructure
endStructure

@Event: onSendMessage:%messageSend%
if:'%messageSend%'='/asas on'
cancelSendMessage
if:'%on%'='0'
var:on;1
save:%on%;on
addMessage:§8Asas §2Activas §8!
else
addMessage:§8Asas §2Já ativas §8!
endStructure
endStructure
if:'%messageSend%'='/asas off'
cancelSendMessage
if:'%on%'='1'
var:on;0
save:%on%;on
addMessage:§8§8Asas §4Desativadas §8!
else
addMessage:§8Asas §4§4Já desativadas §8!
endStructure
endStructure
if:'%messageSend%'='/asas help'
cancelSendMessage
callMethod:help
endStructure
startWith:/asas key;%messageSend%;keyTrue
if:%keyTrue% === true
cancelSendMessage
startSubstring:10;%messageSend%;key
save:%key%;key
addMessage:§8Sua chave para mudar seu ponto de vista é: : §B%key% §8!
endStructure

