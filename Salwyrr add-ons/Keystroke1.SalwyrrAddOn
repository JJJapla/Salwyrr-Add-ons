@Event: onKeyPressed:%pressedKey%
if:"%pressedKey%" == "%keyUp%"
var:isUpPressed;true
endStructure
if:"%pressedKey%" == "%keyLeft%"
var:isLeftPressed;true
endStructure
if:"%pressedKey%" == "%keyDown%"
var:isDownPressed;true
endStructure
if:"%pressedKey%" == "%keyRight%"
var:isRightPressed;true
endStructure
if:"%pressedKey%" == "%keyJump%"
var:isJumpPressed;true
endStructure
if:"%pressedKey%" == "%keySneak%"
var:isSneakPressed;true
endStructure
if:"%pressedKey%" == "%keyLMB%"
var:isLMBPressed;true
endStructure
if:"%pressedKey%" == "%keyRMB%"
var:isRMBPressed;true
endStructure

@Event: onKeyUnpressed:%unpressedKey%
if:"%unpressedKey%" == "%keyUp%"
var:isUpPressed;false
endStructure
if:"%unpressedKey%" == "%keyLeft%"
var:isLeftPressed;false
endStructure
if:"%unpressedKey%" == "%keyDown%"
var:isDownPressed;false
endStructure
if:"%unpressedKey%" == "%keyRight%"
var:isRightPressed;false
endStructure
if:"%unpressedKey%" == "%keyJump%"
var:isJumpPressed;false
endStructure
if:"%unpressedKey%" == "%keySneak%"
var:isSneakPressed;false
endStructure
if:"%unpressedKey%" == "%keyLMB%"
var:isLMBPressed;false
endStructure
if:"%unpressedKey%" == "%keyRMB%"
var:isRMBPressed;false
endStructure

@Event: onAddonStart
var:showJumpSneak;true
var:showMouse;true
var:keyUp;W
var:keyLeft;A
var:keyDown;S
var:keyRight;D
var:keyJump;SPACE
var:keySneak;LSHIFT
var:keyLMB;Bouton 1
var:keyRMB;Bouton 2
var:width;25
var:bgColor;black
var:pressColor;chroma
var:isUpPressed;false
var:isDownPressed;false
var:isLeftPressed;false
var:isRightPressed;false
var:isJumpPressed;false
var:isSneakPressed;false
var:isLMBPressed;false
var:isRMBPressed;false
var:posX;%screenWidth% - (%width% + 1) * 3  - 5
var:posY;5
getSave:showJumpSneak;showJumpSneak
getSave:showMouse;showMouse
getSave:bgColor;bgColor
getSave:pressColor;pressColor

@Event: onSendMessage:%messageSend%
if:"%messageSend%"="/ksespaco on"
cancelSendMessage
if:"%showJumpSneak%"="false"
addMessage:§AEspaço do KeyStrokes ativado!
var:showJumpSneak;true
save:%showJumpSneak%;showJumpSneak
else
addMessage:§AJá ativado!
endStructure
endStructure
if:"%messageSend%"="/ksespaco off"
cancelSendMessage
if:"%showJumpSneak%"="true"
addMessage:§cEspaço do KeyStrokes desativado!
var:showJumpSneak;false
save:%showJumpSneak%;showJumpSneak
else
addMessage:§cJá desativado!
endStructure
endStructure
if:"%messageSend%"="/ksclick on"
cancelSendMessage
if:"%showMouse%"="false"
addMessage:§AClicks do KeyStrokes ativado!
var:showMouse;true
save:%showMouse%;showMouse
else
addMessage:§AJá ativado!
endStructure
endStructure
if:"%messageSend%"="/ksclick off"
cancelSendMessage
if:"%showMouse%"="true"
addMessage:§cClicks do KeyStrokes desativado!
var:showMouse;false
save:%showMouse%;showMouse
else
addMessage:§cJá desativado!
endStructure
endStructure
startWith:/ksbg ;%messageSend%;ksbgTrue
if:"%ksbgTrue%"="true"
cancelSendMessage
startSubstring:6;%messageSend%;bgColor
save:%bgColor%;bgColor
addMessage:§6Cor do BackGround: %bgColor%
endStructure
startWith:/kstext ;%messageSend%;kstextTrue
if:"%kstextTrue%"="true"
cancelSendMessage
startSubstring:8;%messageSend%;pressColor
save:%pressColor%;pressColor
addMessage:§6Cor do Texto: %pressColor%
endStructure

#Method:drawKey
var:bg;%bgColor%
if:%pressed%
var:bg;%pressColor%
endStructure
var:text;%pressColor%
if:%pressed%
var:text;%bgColor%
endStructure
drawRect:%x%;%y%;%keyWidth%;%keyHeight%;%bg%
drawCenteredText:%key%;%x% + %keyWidth% / 2;%y% + %keyHeight% / 2 - 4;%text%
@MethodEnd

@Event: onDraw
var:keyWidth;%width%
var:keyHeight;%width%
var:key;%keyUp%
var:x;%posX% + %width% + 1
var:y;%posY%
var:pressed;%isUpPressed%
callMethod:drawKey
var:key;%keyLeft%
var:x;%posX%
increment:y;%keyHeight% + 1
var:pressed;%isLeftPressed%
callMethod:drawKey
var:key;%keyDown%
var:x;%posX% + %width% + 1
var:pressed;%isDownPressed%
callMethod:drawKey
var:key;%keyRight%
var:x;%posX% + %width% * 2 + 2
var:pressed;%isRightPressed%
callMethod:drawKey
if:%showJumpSneak%
var:key;⬆
var:x;%posX%
increment:y;%keyHeight% + 1
var:pressed;%isSneakPressed%
var:keyHeight;%width% / 1.25
callMethod:drawKey
var:keyWidth;%width% * 2 + 1
var:key;§m-----
var:x;%posX% + %width% + 1
var:pressed;%isJumpPressed%
callMethod:drawKey
endStructure
if:%showMouse%
var:key;LMB
increment:y;%keyHeight% + 1
var:keyHeight;%width% / 1.25
var:x;%posX%
var:keyWidth;%width% * 1.5
var:pressed;%isLMBPressed%
callMethod:drawKey
var:key;RMB
var:x;%posX% + %keyWidth% + 1
var:keyWidth;((%width%) * 3 + 2) - %keyWidth% - 1
var:pressed;%isRMBPressed%
callMethod:drawKey
endStructure

